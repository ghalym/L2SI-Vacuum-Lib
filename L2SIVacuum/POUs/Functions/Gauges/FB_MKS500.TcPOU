<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.3">
  <POU Name="FB_MKS500" Id="{7585aac8-a989-4785-918b-438600204b3a}" SpecialFunc="None">
    <Declaration><![CDATA[(* This function is for the Cold Cathode MKS 500 connected to Beckhoff EL terminals*)
(* 500 Logarithmic Output Conversion, factory default configuration *)
(*This function provides ILK and Set Point Protection for the Cold Cathode*)
FUNCTION_BLOCK FB_MKS500 EXTENDS FB_GaugeBase
VAR_IN_OUT

END_VAR
VAR_INPUT
	PG	:	ST_VG; // Pirani Gauge Structure used to Interlock the Cold Cathode
END_VAR
VAR_OUTPUT
	{attribute 'pytmc' := '
    pv: 
	'}
	IG : ST_VG; // The Cold Cathode Data Structure
END_VAR
VAR
	rV : REAL;
	pBase : REAL :=1.0E-10;//default curve base pressure is 1E-10. Confusing since can't actually read that low using analog out.
	vBase : REAL := 0; // corresponding vbase 0V based on default curve.  See manual pg. 44
	vSlope : REAL := 1.0; //looks correct for default curve
	GaugeTurnOnTmr : TON;
	MinPressure: REAL := 1E-10;
	cDefaultPressure : REAL := 0;
	timer:TON;
	iTermBits: UINT := 32767 ; // The terminal's maximum value in bits
	(*IOs to be linked*)
	/// Controls and I/Os
	i_iPRESS_R AT %I* :INT; // input Pressure // Link to analog Input
	i_xHV_ON AT %I*	: BOOL; //  True when High Voltage is on  // 'TcLinkTo' (EL1124) ^Input
	i_xDisc_Active AT %I* : BOOL;// Discharge Current Active // 'TcLinkTo' (EL1124) ^Input
	q_xHV_DIS AT %Q* : BOOL; // Enable High Voltage when True // 'TcLinkTo' (EL2624) ^Output
	//
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* 500 Logarithmic Output Conversion, factory default configuration *)
If (iTermBits=0) THEN iTermBits := 32767;END_IF
rV := 10*INT_TO_REAL(IG.i_iPRESS_R)/iTermBits;

(*
IF rV >= 0.49 AND rV < 0.52  THEN
	IG.wState := 5;
	IG.rPRESS := cDefaultPressure;
ELSIF rV < 0.4 THEN
	IG.wState := 4;//HV Off/ gauge disconnected
	IG.rPRESS := cDefaultPressure;
ELSE
	IG.wState := 1;
	IG.rPRESS := LREAL_TO_REAL(EXPT(10,((rV-vBase)/vSlope+LOG(pBase))));
END_IF
*)

(* Set Guage State based on the inputs HV ON and Discharge current*)
IF IG.i_xHV_ON AND NOT IG.i_xDisc_Active THEN
	IG.eState := Starting;
	IG.rPRESS := cDefaultPressure;
ELSIF IG.i_xHV_ON AND IG.i_xDisc_Active THEN
	IG.eState := Valid;
	IG.rPRESS := LREAL_TO_REAL(EXPT(10,((rV-vBase)/vSlope+LOG(pBase))));
ELSIF NOT IG.i_xHV_ON AND NOT IG.i_xDisc_Active THEN
	IG.eState := Off;
	IG.rPRESS := cDefaultPressure;
END_IF




(* Ion Gauge Protection Functions *)
(* If the PG pressure is greater than the VG.PRO_SP then the gauge is disabled *)
(* If the PG pressure is less than the VG.PRO_SP then the gauge is enabled *)
(* This FB also implements some hysteresis so the gauge doesn't have rapid power cycling while near the turn on boundary *)



IF PG.rPRESS <= IG.rPRO_SP AND PG.xPRESS_OK THEN
	IG.q_xHV_DIS := NOT (IG.xHV_SW);
	IG.xILKOk := TRUE;
ELSIF NOT IG.q_xHV_DIS AND timer.Q THEN 
	IF IG.rPRESS > (IG.rPRO_SP + IG.rHYS_PR)  OR PG.rPRESS > (IG.rPRO_SP + IG.rHYS_PR) THEN
		IG.q_xHV_DIS := TRUE;
		IG.xHV_SW := FALSE;
	END_IF
ELSE
	IG.q_xHV_DIS := TRUE;
	IG.xHV_SW := FALSE;
	IG.xILKOk := FALSE;
END_IF

(* Pressure gauge OK checks *)

GaugeTurnOnTmr(IN:=IG.q_xHV_DIS, PT:=T#10S, Q=>IG.xTurnOnTime);


//Backwards compatibility
IG.xPRESS_OK := (IG.eState = Valid) OR IG.xBAKEOUT;

(* Setpoint evaluation *)
IG.xAT_VAC := (IG.xPRESS_OK AND (IG.rPRESS < IG.rVAC_SP)) OR IG.xBAKEOUT AND (IG.eState = Valid) ;

(*Inlk Evaluation*)
IG.xILKOk := (PG.rPRESS <= IG.rPRO_SP AND PG.xPRESS_OK);

(*Logging*)

This^.ACT_Logger();

(*Soft IO Linking*)
// check ethercat Diagnostics
IO();
timer(IN:= NOT IG.q_xHV_DIS, PT:= T#2s);

]]></ST>
    </Implementation>
    <Action Name="ACT_Logger" Id="{033b99e8-5810-4d8e-9cf8-3bcae5408eda}">
      <Implementation>
        <ST><![CDATA[// ILK logger
IF NOT IG.xILKOk AND NOT IG.q_xHV_DIS THEN
 		fbLogger(sMsg:='Lost external interlock while gauge was on.', eSevr:=TcEventSeverity.Critical);  	
END_IF


// Log Action
tAction(CLK:=  IG.q_xHV_DIS);
IF tAction.Q THEN fbLogger(sMsg:='Ion gauge commanded to switch on', eSevr:=TcEventSeverity.Info); END_IF



//STATE Logger  
IF ePrevState <> IG.eState THEN
	  CASE IG.eState OF
		ValidHi:
			fbLogger(sMsg:='Gauge pressure valid high.', eSevr:=TcEventSeverity.Info);
		ValidLo:
			fbLogger(sMsg:='Gauge pressure valid low.', eSevr:=TcEventSeverity.Info);
		Valid:
			fbLogger(sMsg:='Gauge pressure valid.', eSevr:=TcEventSeverity.Info);
  		GaugeDisconnected:
			fbLogger(sMsg:='Gauge Disconnected.', eSevr:=TcEventSeverity.Critical);
		PressInvalid:
			fbLogger(sMsg:='Gauge pressure invalid.', eSevr:=TcEventSeverity.Warning);
		OoR:
			fbLogger(sMsg:='Gauge pressure out of range.', eSevr:=TcEventSeverity.Warning);
		Starting:
			fbLogger(sMsg:='Gauge starting.', eSevr:=TcEventSeverity.Info);
	  END_CASE
	  ePrevState := IG.eState;  
  END_IF
  

]]></ST>
      </Implementation>
    </Action>
    <Action Name="IO" Id="{d5dcbb30-1e72-4867-8faf-91e37cf07851}">
      <Implementation>
        <ST><![CDATA[(*check health of the ethercat terminal*) //todo
(*soft link inputs*)
IG.i_iPRESS_R:=	i_iPRESS_R; 
IG.i_xHV_ON:= i_xHV_ON;
IG.i_xDisc_Active:=i_xDisc_Active; 
(*soft link outputs*)
q_xHV_DIS := IG.q_xHV_DIS;]]></ST>
      </Implementation>
    </Action>
    <Method Name="M_AutoOn" Id="{ca22a2df-417e-4d6a-bc4d-3fb0fe75c5bf}">
      <Declaration><![CDATA[METHOD M_AutoOn : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[this^.IG.xHV_SW:= TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_HVE" Id="{c11659a5-8a50-4170-9e43-162518265fbf}">
      <Declaration><![CDATA[METHOD M_HVE : BOOL
VAR_INPUT
		enable:bool; // set to true to enable, false to disable;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
this^.IG.xHV_SW:= (enable);]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_SetBits" Id="{045c52f1-d059-4db3-a35f-c5604ec4a648}">
      <Declaration><![CDATA[METHOD M_SetBits : BOOL
VAR_INPUT
	TermBits : UINT; // The terminal's maximum value in bits
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[This^.iTermBits := TermBits;]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>